'rWallpaper VBS Script
' Written by Adam "Arcath" Laycock
' June 2010
' Generated by rWallpaper <%= Time.now %>

'Objects and Variables
screen = ScreenResolution
Set WshNetwork = WScript.CreateObject("WScript.Network")
uname=Replace(WshNetwork.UserName,".","&dot;")
Set objFS=CreateObject("Scripting.FileSystemObject")
dim xHttp: Set xHttp = createobject("Microsoft.XMLHTTP")
dim bStrm: Set bStrm = createobject("Adodb.Stream")

'Functions
'Screen Resolution
Function ScreenResolution()
    Set oIE = CreateObject("InternetExplorer.Application")
    With oIE
        .Navigate("about:blank")
    Do Until .readyState = 4: wscript.sleep 100: Loop
        width = .document.ParentWindow.screen.width
        height = .document.ParentWindow.screen.height
    End With
    oIE.Quit
    ScreenResolution = array(width,height)
End Function

'Set the URL
url = "<%= Setting.find_by_key("ru").value %>wallpapers/" & WshNetwork.ComputerName & "/" & uname & "/" & screen(0) & "/" & screen(1) & ".jpg"

'Create the rWallpaper Folder if it doesnt exist
if objFS.FolderExists("C:\rWallpaper") Then
    'Folder Exists Do Nothing
else
    'Create the Folder
    objFS.CreateFolder("C:\rWallpaper")
end if

'Download the Wallpaper
xHttp.Open "GET", url, False
xHttp.Send

with bStrm
    .type = 1 '//binary
    .open
    .write xHttp.responseBody
    .savetofile "C:\rWallpaper\wallpaper.jpg", 2 '//overwrite
end with
